use std::collections::HashMap;
use std::iter::FromIterator;

use crate::lexer::{ParseError, Token};

grammar;

extern {
  type Location = usize;
  type Error = ParseError;

  enum Token {
    "=" => Token::Eq,
    "export" => Token::Export,
    "ident" => Token::Ident(<String>),
    "qs" => Token::QuotedString(<String>),
    "\n" => Token::NewLine,
    "EOF" => Token::EOF,
  }
}

pub EnvFile: HashMap<String, String> = {
  <Line*> => HashMap::from_iter(<>.into_iter().filter_map(|x| x)),
}

Line: Option<(String, String)> = {
  <k:Key> "=" <v:Value> => Some((k, v)),
  "\n" => None,
  "EOF" => None,
};

Key: String = "export"? <k:"ident"> => k;

Value: String = {
  "ident" => <>,
  "qs" => <>,
  "\n" => String::new(),
  "EOF" => String::new(),
};
